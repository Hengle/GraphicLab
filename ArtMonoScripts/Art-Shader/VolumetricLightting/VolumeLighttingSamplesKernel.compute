#pragma kernel UpdateEpipolarTex
#pragma kernel LerpWork
#pragma kernel ResPlane
#pragma kernel TransformPlane
#pragma kernel RayMarching
#define PI 3.141592653
#define PI4 12.56637061
RWTexture2D<float4> EpipolarTex;
float DepthThreshold;
float2 SourceCoord;
float4 ScreenParams;
float4 EpipolarParams;
float4 CamParams;
float4 lightPos,WorldCamPos;
float4x4 CamToWorld;
Texture2D<float> DepthBuffer;
float2 DepthBufferSize;
float4 RectTexSize;
float4 mediaParams;
float2 epipolarSize;
float2 BoundaryEpipolarPoint(half indexH)
{
    uint boundary=clamp(floor(indexH*4),0,3);
    float PosOnBoundary=frac(indexH*4);
    float BoundaryCoord=-1 + 2.0*PosOnBoundary;
    float4 BoundaryXCoord=float4(BoundaryCoord,1,-BoundaryCoord,-1);
    float4 BoundaryYCoord=float4(1,-BoundaryCoord,-1,BoundaryCoord);
    half4 BoundaryFlags=half4(boundary==uint4(0,1,2,3));
    return float2(dot(BoundaryXCoord,BoundaryFlags),dot(BoundaryYCoord,BoundaryFlags));
}
float BoundaryEpipolarIndics(float2 exitP,half4 sectorFlags)
{
    float4 indics=float4(0,0.25,0.5,0.75)+ ((0.5+exitP.xyxy*float4(0.5,-0.5,-0.5,0.5))/4.0f);
    return dot(indics,sectorFlags);
}
float2 GetEntryPoint(float2 exitP)
{
    float2 RayDir=exitP-SourceCoord;
    float DistToExitBoundary=length(RayDir);
    RayDir/=DistToExitBoundary;
    float4 IsCorrectIntersectionFlags=abs(RayDir.xyxy)>1e-5;
    float4 DisToBoundaries=(float4(1,1,-1,-1)-SourceCoord.xyxy)/(RayDir.xyxy+(1-IsCorrectIntersectionFlags));
    IsCorrectIntersectionFlags= IsCorrectIntersectionFlags && (DisToBoundaries<(DistToExitBoundary-(1e-4)));
    DisToBoundaries=IsCorrectIntersectionFlags * DisToBoundaries +(1-IsCorrectIntersectionFlags)*float4(-10,-10,-10,-10);
    float FirstIntersecDist=0;
    FirstIntersecDist=max(FirstIntersecDist,DisToBoundaries.x);
    FirstIntersecDist=max(FirstIntersecDist,DisToBoundaries.y);
    FirstIntersecDist=max(FirstIntersecDist,DisToBoundaries.z);
    FirstIntersecDist=max(FirstIntersecDist,DisToBoundaries.w);
    return SourceCoord+RayDir*FirstIntersecDist;
}
float2 ReLocatedExitPoint(float2 exitP,float2 entryP)
{
    float eSliceScreenLen=length((exitP-entryP)*ScreenParams.xy/2);
    return entryP+(exitP-entryP)*max(EpipolarParams.x/eSliceScreenLen,1);
}
groupshared half cache[32][32];
[numthreads(32,32,1)]
void UpdateEpipolarTex(uint2 id:SV_DispatchThreadID,uint2 tid:SV_GroupThreadID)
{
    float2 ExitPoint=BoundaryEpipolarPoint(id.x/epipolarSize.x);
    float2 EntryPoint=GetEntryPoint(ExitPoint);
    ExitPoint=ReLocatedExitPoint(ExitPoint,EntryPoint);
    float2 uv=lerp(EntryPoint,ExitPoint,saturate(id.y/epipolarSize.y) );

    half flag=1-step(0.1f,(id.y%EpipolarParams.y)*1.0f);
    if(abs(uv.x)>(1+1e-4)||abs(uv.y)>(1+1e-4))
    {
        uv=-1;
        flag=0;
    }

    uv=(uv+1)/2;
    float d=DepthBuffer[uv*DepthBufferSize];
    cache[tid.x][tid.y]=d;
    GroupMemoryBarrierWithGroupSync();
    float thr=0;
    if(tid.y>=1)
    {
       thr= cache[tid.x][tid.y]-cache[tid.x][tid.y-1];
       thr=abs(thr);
    }
    EpipolarTex[id]=float4(uv,step(0.5,flag+step(DepthThreshold,thr)),d);
}
Texture2D<float4> input;
Texture2D<float4> samples;
RWTexture2D<float4> outPut;
[numthreads(32,32,1)]
void LerpWork(int2 id:SV_DispatchThreadID)
{
    int m=0;
    while(1)
    {
        m++;
        if(input[int2(id.x,id.y-m)].z>0)
            break;
        if(m>id.y)
            break;
    }
    int n=0;
    while(1)
    {
        n++;
        if(input[int2(id.x,id.y+n)].z>0)
            break;
        if(n>id.y)
            break;
    }
    outPut[id]=lerp(samples[id-int2(0,m)] ,samples[id+int2(0,n)], (m*1.0f/(m+n)));
}
RWTexture2D<float4> SecPlane;
Texture2D<float3> temp;
inline half3 LM(float3 a,float3 b)
{
    return a-b;
}
inline float2 GetExitPoint(float2 uv,out half4 sectorFlags)
{
    float2 rayDir=normalize(uv-SourceCoord);
    float4 halfSpaceEquationTerms=(uv.xxyy-float4(-1,1,-1,1))*rayDir.yyxx;
    half4 halfSpaceFlags=halfSpaceEquationTerms.xyyx<halfSpaceEquationTerms.zzww;
    sectorFlags=halfSpaceFlags.zyxw*(1-halfSpaceFlags.wzyx);
    float4 DistToBoundaries=(float4(1,1,-1,-1)-SourceCoord.yxyx)/(rayDir.yxyx+float4(abs(rayDir.yxyx)<1e-6));
    float DistToExitBoundary=dot(sectorFlags,DistToBoundaries);
    return SourceCoord+rayDir*DistToExitBoundary;   
}
inline half EpipolarSliceIndics(float2 entryP,float2 exitP,float2 P)
{
    float lenP=length(P-entryP);
    float lenE=length(exitP-entryP);
    return lenP/lenE;
}
inline float4 GetWorldPositionFromDepthValue( float2 uv, float linearDepth ) 
{
    float camPosZ = CamParams.x + CamParams.y* linearDepth;
    float height = 2 * camPosZ / CamParams.z;
    float width = CamParams.w * height;
    float camPosX = width * uv.x - width / 2;
    float camPosY = height * uv.y - height / 2;
    float4 camPos = float4(camPosX, camPosY, -camPosZ, 1.0);
    return mul(CamToWorld, camPos);
}
[numthreads(32,32,1)]
void TransformPlane(int2 id:SV_DispatchThreadID)
{
    float2 uv=id*RectTexSize.zw;
    uv=uv*2-1;
    half4 sectF=0;
    float2 exitPoint=GetExitPoint(uv,sectF);
    float2 entryPoint=GetEntryPoint(exitPoint);
    half indics=BoundaryEpipolarIndics(exitPoint,sectF);
    exitPoint=ReLocatedExitPoint(exitPoint,entryPoint);
    float inkX=indics*epipolarSize.x;
    int xMin=floor(inkX);
    float inkY=EpipolarSliceIndics(entryPoint,exitPoint,uv)*epipolarSize.y;
    int yMin=floor(inkY);
    float xRatio=inkX-xMin;
    float yRatio=inkY-yMin;
    half3 horiA=temp[int2(xMin,yMin)];
    int xnext=(xMin+2)>epipolarSize.x?0:xMin+1;
    half3 horiB=temp[int2(xnext,yMin)];
    half3 horiC=temp[int2(xMin,yMin+1)];
    half3 horiD=temp[int2(xnext,yMin+1)];
    //SecPlane[id]=float4(EpipolarSliceIndics(entryPoint,exitPoint,uv),0,0,1);
    //SecPlane[id]=float4(temp[int2(xMin,yMin)].xyz,1);
    SecPlane[id]=float4(lerp(lerp(horiA,horiB,xRatio),lerp(horiC,horiD,xRatio),yRatio),1);
}
float2 sizef;
float2 rectSizef;
RWTexture2D<float4> opt;
[numthreads(8,8,1)]
void ResPlane(int2 id:SV_DispatchThreadID)
{
    float2 oUV=id*sizef;
    float2 nUpos=oUV*RectTexSize.xy;
    int2 lPosMin=floor(nUpos);
    int2 lposA=int2(lPosMin.x,lPosMin.y);
    int2 lposB=int2(lPosMin.x+1,lPosMin.y);
    int2 lposC=int2(lPosMin.x,lPosMin.y+1);
    int2 lposD=int2(lPosMin.x+1,lPosMin.y+1);
    half u_ratio=nUpos.x-lPosMin.x;
    half v_ratio=nUpos.y-lPosMin.y;
    half3 horiA=lerp(temp[lposA].xyz,temp[lposB].xyz,u_ratio);
    half3 horiB=lerp(temp[lposC].xyz,temp[lposD].xyz,u_ratio);
    //opt[id]=float4(lerp(horiA,horiB,v_ratio),1);
    opt[id]=float4(samples[id].xyz+lerp(horiA,horiB,v_ratio),1);
}
RWTexture2D<float4> RaymarchingRes;
float4x4 shadowMat;
float4 shadowProjParams,DParams;
int SampleCount;
float4 scatterColor;
half GetAtten(float3 pos)
{
    float4 wpos=float4(pos,1);
    float4 spos=mul(shadowMat,wpos);
    spos.xyz/=spos.w;
    float atten=(spos.x>-1)*(spos.x<1)*(spos.y>-1)*(spos.y<1)*(spos.z<1)*(spos.z>-1);
    float z=DepthBuffer[(spos.xy*0.5+0.5)*DepthBufferSize].r;
    float d=shadowProjParams.z*z;
    atten*=(d>spos.w);
    //atten*=(1/spos.w);
    return atten;
}
float MieScattering(float cosTheta)
{
    float G2=mediaParams.z*mediaParams.z;
    return(1-G2)/(PI4*pow(abs(1+G2+2*mediaParams.z*cosTheta),1.5));
}
[numthreads(32,32,1)]
void RayMarching(uint2 id:SV_DispatchThreadID)
{
    float4 msg=input[id];
    if(1-step(0.1f,msg.z))
        return;
    float d=1/(DParams.x*msg.w+DParams.y);
    float3 wpos=GetWorldPositionFromDepthValue(msg.xy,d).xyz;
    float3 st=(wpos-WorldCamPos.xyz)/SampleCount;
    float3 currentPos=WorldCamPos.xyz;
    float len=length(st);
    float extinction=0;
    float3 res=0;
    [loop]
    for(int i=0;i<SampleCount;++i)
    {
        currentPos =WorldCamPos.xyz+i*st;
        float density=1;// prepare for noise 
        float scattering=mediaParams.x*len*density;
        extinction+=mediaParams.y*len*density;


        float3 tolight=lightPos.xyz-currentPos;
        float Costheta=dot(normalize(st),tolight);

        float3 il= scatterColor.xyz*GetAtten(currentPos);
        il=il*scattering*exp(-extinction);

        il*=MieScattering(Costheta);

        res+=il;
    }
    
    RaymarchingRes[id]=float4(res,1);
}